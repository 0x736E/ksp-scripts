//
// Config
//

SET countDownTimer TO 10.
SET stageCount TO 2.

SET departureAngleVelocity TO 100.
SET departureAngle TO (UP + R(20,0,0)).

SET lowerAtmosphereSpeedMin TO 155.
SET lowerAtmosphereSpeedMax TO 160.
SET highAltitudeBurnTarget TO 10000.
SET lowerAtmosphereMinThrottle TO 0.1.

SET apoapsisBurnTime TO 10.

SET apoapsisTarget TO 75000.
SET periapsisTarget TO 80000.
SET periapsisFullBurn TO 30000.


//
// Private
//

SET _beginOrbitManuever TO FALSE.
SET _minOrbitAltitude TO 70000.
SET _lowerAtmosphereSpeedMod TO highAltitudeBurnTarget-200.

//
// CountDownTimer
//

PRINT "LAUNCH in " + countDownTimer + "...".
UNTIL (countDownTimer = 0) {
	PRINT countDownTimer.
	SET countDownTimer TO (countDownTimer -1).
	WAIT 1.
}

// 
// SETUP
//

SET SHIP:CONTROL:PILOTMAINTHROTTLE to 1.
LOCK STEERING TO UP.
STAGE.
PRINT "Launch!".

//
// Stage when fuel is low
//

WHEN (STAGE:LIQUIDFUEL < 0.1) AND (SHIP:ALTITUDE < apoapsisTarget) THEN {

	LIST ENGINES IN _engines.
	SET eng TO _engines[ _engines:LENGTH-1 ].

	if(eng:IGNITION = TRUE) {

		PRINT "FUEL STAGE COMPLETE".
		SET stageCount TO stageCount - 1.
		STAGE.

		if (stageCount > 0) {

			PRESERVE.

		} else {

			PRINT "Final stage.".
		}

	}

	PRESERVE.

}

//
//
//

WHEN (ETA:APOAPSIS > 20) AND (ETA:APOAPSIS < 30) THEN {
	
	PRINT "BEGINNING APOAPSIS BURN SEQUENCE".

}

// 
// Shutdown engine when <= 0.1
//

WHEN (SHIP:CONTROL:PILOTMAINTHROTTLE <= 0.1) THEN {

	LIST ENGINES IN _engines.
	SET eng TO _engines[ _engines:LENGTH-1 ].

	if(eng:IGNITION = TRUE) {
		eng:SHUTDOWN.
	}

	PRESERVE.

}

// 
// Activate engine when at 0.1
//

WHEN (SHIP:CONTROL:PILOTMAINTHROTTLE >= 0.1) THEN {
	
	LIST ENGINES IN _engines.
	SET eng TO _engines[ _engines:LENGTH-1 ].

	if(eng:IGNITION = FALSE) {
		eng:ACTIVATE.
	}

	PRESERVE.
	
}

//
// Set departure angle
//

WHEN (SHIP:VELOCITY:SURFACE:Z >= departureAngleVelocity) THEN {

	PRINT "LOCK ORBITAL DEPARTURE ANGLE".
	LOCK STEERING TO departureAngle.

}

//
// Initiate high altitude burn
//
WHEN (SHIP:ALTITUDE >= highAltitudeBurnTarget) THEN {

	PRINT "HIGH ALTITUDE BURN".
	SET SHIP:CONTROL:PILOTMAINTHROTTLE TO 1.

}

//
// Coast ship until T-30s from apoapsis
//

WHEN (APOAPSIS >= apoapsisTarget) THEN {

	PRINT "COASTING UNTIL APOAPSIS T-30s".
	SET _beginOrbitManuever TO TRUE.
	LOCK STEERING TO PROGRADE.

}

//
// PROGRAM TERMINUS
//

WHEN (PERIAPSIS >= periapsisTarget) THEN {

	PRINT "ESTABLISHED ORBIT".

	LIST ENGINES IN _engines.
	SET eng TO _engines[ _engines:LENGTH-1 ].

	if(eng:IGNITION = TRUE) {
		eng:SHUTDOWN.
	}

	SET SHIP:CONTROL:PILOTMAINTHROTTLE TO 0.01.
	LOCK STEERING TO PROGRADE.

	UNLOCK STEERING.
	UNLOCK THROTTLE.
	
	// end program

}


//
// Modulate speed in lower atmosphere 
// target is 300m/s
//

UNTIL (SHIP:ALTITUDE >= _lowerAtmosphereSpeedMod) {
	
	if (SHIP:CONTROL:PILOTMAINTHROTTLE > lowerAtmosphereMinThrottle) {

		if (SHIP:CONTROL:PILOTMAINTHROTTLE < 1) AND (SHIP:VELOCITY:SURFACE:Z <= lowerAtmosphereSpeedMin) {

			SET SHIP:CONTROL:PILOTMAINTHROTTLE TO (SHIP:CONTROL:PILOTMAINTHROTTLE + 0.01).

		} else if (SHIP:CONTROL:PILOTMAINTHROTTLE > 0.01) AND (SHIP:VELOCITY:SURFACE:Z >= lowerAtmosphereSpeedMax) {

			SET SHIP:CONTROL:PILOTMAINTHROTTLE TO (SHIP:CONTROL:PILOTMAINTHROTTLE - 0.01).
		}

	} else {

		SET SHIP:CONTROL:PILOTMAINTHROTTLE TO lowerAtmosphereMinThrottle.

	}

}

//
// Modulate speed to achieve orbit
//

UNTIL (PERIAPSIS >= periapsisTarget) {

	if (_beginOrbitManuever = TRUE) {

		//
		// Modulate thrusters to achieve orbit
		//
		if(PERIAPSIS < periapsisFullBurn) {

			// ETA is too close!
			// Adjust APOAPSIS
			//
			if(ETA:APOAPSIS <= 20) {

				LOCK STEERING TO UP.
				SET SHIP:CONTROL:PILOTMAINTHROTTLE TO 1.

			//
			// Modulate thrusters to achieve orbit
			//
			} else {

				if (SHIP:CONTROL:PILOTMAINTHROTTLE < 1) AND (ETA:APOAPSIS <= 30) {

					LOCK STEERING TO PROGRADE.
					SET SHIP:CONTROL:PILOTMAINTHROTTLE TO (SHIP:CONTROL:PILOTMAINTHROTTLE + 0.01).

				} else if (SHIP:CONTROL:PILOTMAINTHROTTLE > 0.01) AND (ETA:APOAPSIS >= 30+apoapsisBurnTime) {

					SET SHIP:CONTROL:PILOTMAINTHROTTLE TO (SHIP:CONTROL:PILOTMAINTHROTTLE - 0.01).
				}

			}

		//
		// PERIAPSIS full burn
		//
		} else if (PERIAPSIS < periapsisTarget) AND (SHIP:CONTROL:PILOTMAINTHROTTLE < 1) {

			LOCK STEERING TO PROGRADE.
			SET SHIP:CONTROL:PILOTMAINTHROTTLE TO 1.

		}

	}

}