
SET _stageCount TO 2.
SET beginOrbitManuever TO FALSE.


PRINT "LAUNCH in 5...".

// 5s countdown
SET _launchCount TO 5.
UNTIL (_launchCount = 0) {
	PRINT _launchCount.
	SET _launchCount TO _launchCount -1.
	wait 1.
}

SET SHIP:CONTROL:PILOTMAINTHROTTLE to 1.
LOCK STEERING TO UP.
STAGE.
PRINT "Launch!".

WHEN (STAGE:LIQUIDFUEL < 0.1) THEN {

	PRINT "FUEL STAGE COMPLETE".
	SET _stageCount TO _stageCount - 1.
	STAGE.

	if (_stageCount > 0) {
		PRESERVE.
	} else {
		PRINT "Final stage.".
	}

}

WHEN (SHIP:VELOCITY:SURFACE:Z > 100) THEN {

	PRINT "LOCK ORBITAL DEPARTURE ANGLE".
	LOCK STEERING TO UP + R(20,0,0).

}

WHEN (SHIP:ALTITUDE > 10000) THEN {

	PRINT "HIGH ALTITUDE BURN".
	SET SHIP:CONTROL:PILOTMAINTHROTTLE TO 1.

}

WHEN (APOAPSIS > 75000) THEN {

	PRINT "COASTING UNTIL APOAPSIS T-30s".
	SET beginOrbitManuever TO TRUE.
	LOCK STEERING TO PROGRADE.

}

// Modulate speed in lower atmosphere 
// target is 300m/s
UNTIL (SHIP:ALTITUDE > 9800) {
	
	if (SHIP:CONTROL:PILOTMAINTHROTTLE < 1) AND (SHIP:VELOCITY:SURFACE:Z < 160) {

		PRINT "MODULATE SPEED UP".
		SET SHIP:CONTROL:PILOTMAINTHROTTLE TO (SHIP:CONTROL:PILOTMAINTHROTTLE + 0.01).

	} else if (SHIP:CONTROL:PILOTMAINTHROTTLE > 0) AND (SHIP:VELOCITY:SURFACE:Z > 165) {

		PRINT "MODULATE SPEED DOWN".
		SET SHIP:CONTROL:PILOTMAINTHROTTLE TO (SHIP:CONTROL:PILOTMAINTHROTTLE - 0.01).
	}

}

// Modulate speed to achieve orbit
//
SET beginFixAoapsis TO FALSE.
UNTIL (PERIAPSIS > 80000) {

	if (beginOrbitManuever = TRUE) {

		if(PERIAPSIS < 30000) {

			// ETA is too close!
			//
			if(ETA:APOAPSIS < 20) {

				PRINT "APOAPSIS too close, adjusting...".
				LOCK STEERING TO UP.
				SET SHIP:CONTROL:PILOTMAINTHROTTLE TO 1.

			// Modulate thrusters to achieve orbit
			} else {

				if (SHIP:CONTROL:PILOTMAINTHROTTLE < 1) AND (ETA:APOAPSIS < 30) {

					PRINT "APOAPSIS BURN".
					LOCK STEERING TO PROGRADE.
					SET SHIP:CONTROL:PILOTMAINTHROTTLE TO (SHIP:CONTROL:PILOTMAINTHROTTLE + 0.01).

				} else if (SHIP:CONTROL:PILOTMAINTHROTTLE > 0) AND (ETA:APOAPSIS > 40) {

					SET SHIP:CONTROL:PILOTMAINTHROTTLE TO (SHIP:CONTROL:PILOTMAINTHROTTLE - 0.01).
				}

			}

		} else {

			PRINT "PERIAPSIS BURN".
			SET SHIP:CONTROL:PILOTMAINTHROTTLE TO 1.

		}

	}

}

WAIT UNTIL (PERIAPSIS > 70000).
PRINT "ESTABLISHED ORBIT".

WAIT UNTIL (PERIAPSIS > 80000).
SET SHIP:CONTROL:PILOTMAINTHROTTLE TO 0.
UNLOCK STEERING.
UNLOCK THROTTLE.
// end program
